plugins {
	id 'fabric-loom' version '1.6-SNAPSHOT'
	id 'maven-publish'
}

repositories {
	maven {
		name = 'G4me4u Maven'
		url = 'https://maven.g4mesoft.com/snapshots'
	}
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

archivesBaseName = project.archives_base_name
version = project.mod_version + "-mc" + project.minecraft_version
group = project.maven_group

configurations {
    includeModImplementation

    include.extendsFrom includeModImplementation
    modImplementation.extendsFrom includeModImplementation
}

dependencies {
	//to change the versions see the gradle.properties file
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
	
	modImplementation "com.g4mesoft:g4mespeed:${project.g4mespeed_core_version}-mc${project.minecraft_version}-SNAPSHOT"
	includeModImplementation "com.g4mesoft.ui:g4mespeed-ui:${project.g4mespeed_ui_version}-mc${project.minecraft_version}-SNAPSHOT"
}

configurations.all {
	// Ensure we redownload G4mespeed core SNAPSHOT
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.mod_version
	}
}

tasks.withType(JavaCompile).configureEach {
	// ensure that the encoding is set to UTF-8, no matter what the system default is
	// this fixes some edge cases with special characters not displaying correctly
	// see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
	// If Javadoc is generated, this must be specified in that task too.
	it.options.encoding = "UTF-8"
}

jar {
	from "LICENSE"
}
