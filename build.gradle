plugins {
	id 'fabric-loom' version '1.6-SNAPSHOT'
	id 'maven-publish'
}

repositories {
	maven {
		name = 'G4me4u Maven'
		url = 'https://maven.g4mesoft.com/releases'
	}
}

version = "${project.mod_version}-mc${project.minecraft_version}"
group = project.maven_group

base {
	archivesName = project.archives_base_name
}

// See https://github.com/TerraformersMC/GradleScripts/blob/2.5/ferry.gradle

ext.includeMod = { Object dep ->
    dependencies.modImplementation dep
    dependencies.include dep
}

dependencies {
	//to change the versions see the gradle.properties file
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
	
	modImplementation "com.g4mesoft:g4mespeed:${project.g4mespeed_core_version}-mc${project.minecraft_version}"
	includeMod "com.g4mesoft.ui:g4mespeed-ui:${project.g4mespeed_ui_version}-mc${project.minecraft_version}"
}

configurations.all {
	// Ensure we redownload G4mespeed core SNAPSHOT
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 21
}

java {
	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
	// if it is present.
	// If you remove this line, sources will not be generated.
	withSourcesJar()

	sourceCompatibility = JavaVersion.VERSION_21
	targetCompatibility = JavaVersion.VERSION_21
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.base.archivesName.get()}"}
	}
}
